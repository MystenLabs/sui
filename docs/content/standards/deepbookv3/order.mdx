---
title: Order
---

Users can create limit or market orders, modify orders, and cancel orders. The BalanceManager must have the necessary funds to process orders. DeepBook has four order options and three self matching options. Initially, all trading fees will be paid with the DEEP token. If the `pay_with_deep` flag is set to false, orders will automatically fail until the following upgrade. In the initial version, all orders will use DEEP as payment for fees.

Users can modify their existing order, reducing the size and/or lowering the expiration time. Users cannot modify their order to increase their size or increase their expiration time. To do that, they must cancel the original order and place a new order.

Users can cancel a single order or cancel all of their orders.

## Pool Write API

Following are the order related endpoints that `Pool` exposes.

---

### Order Options

```rust
// Restrictions on limit orders.
// No restriction on the order.
const NO_RESTRICTION: u8 = 0;
// Mandates that whatever amount of an order that can be executed in the 
// current transaction, be filled and then the rest of the order canceled.
const IMMEDIATE_OR_CANCEL: u8 = 1;
// Mandates that the entire order size be filled in the current transaction. 
// Otherwise, the order is canceled.
const FILL_OR_KILL: u8 = 2;
// Mandates that the entire order be passive. Otherwise, cancel the order.
const POST_ONLY: u8 = 3;
```

### Self Matching Options

```rust
// Self matching types.
// Self matching is allowed.
const SELF_MATCHING_ALLOWED: u8 = 0;
// Cancel the taker order.
const CANCEL_TAKER: u8 = 1;
// Cancel the maker order.
const CANCEL_MAKER: u8 = 2;
```

### Order Info Struct

Placing a limit order or a market order will return an OrderInfo object. This object is auto dropped. It can be used to inspect the execution details of the request. DBv3 does not catch any errors, if thereâ€™s a failure of any kind, the transaction will fail.

```rust
/// OrderInfo struct represents all order information.
/// This objects gets created at the beginning of the order lifecycle and
/// gets updated until it is completed or placed in the book.
/// It is returned at the end of the order lifecycle.
public struct OrderInfo has store, drop {
    // ID of the pool
    pool_id: ID,
    // ID of the order within the pool
    order_id: u128,
    // ID of the account the order uses
    balance_manager_id: ID,
    // ID of the order defined by client
    client_order_id: u64,
    // Trader of the order
    trader: address,
    // Order type, NO_RESTRICTION, IMMEDIATE_OR_CANCEL, FILL_OR_KILL, POST_ONLY
    order_type: u8,
    // Self matching option,
    self_matching_option: u8,
    // Price, only used for limit orders
    price: u64,
    // Whether the order is a buy or a sell
    is_bid: bool,
    // Quantity (in base asset terms) when the order is placed
    original_quantity: u64,
    // Deep conversion used by the order
    order_deep_price: OrderDeepPrice,
    // Expiration timestamp in ms
    expire_timestamp: u64,
    // Quantity executed so far
    executed_quantity: u64,
    // Cumulative quote quantity executed so far
    cumulative_quote_quantity: u64,
    // Any partial fills
    fills: vector<Fill>,
    // Whether the fee is in DEEP terms
    fee_is_deep: bool,
    // Fees paid so far in base/quote/DEEP terms
    paid_fees: u64,
    epoch: u64,
    // Status of the order
    status: u8,
    // Is a market_order
    market_order: bool,
}

/// The conversion rate of DEEP at the time the order was placed
public struct OrderDeepPrice has copy, store, drop {
    asset_is_base: bool,
    deep_per_asset: u64,
}

/// Fill struct represents the results of a match between two orders.
/// It is used to update the state.
public struct Fill has store, drop, copy {
    // ID of the maker order
    maker_order_id: u128,
    // account_id of the maker order
    balance_manager_id: ID,
    // Whether the maker order is expired
    expired: bool,
    // Whether the maker order is fully filled
    completed: bool,
    // Quantity filled
    base_quantity: u64,
    // Quantity of quote currency filled
    quote_quantity: u64,
    // Whether the taker is bid
    taker_is_bid: bool,
    // Maker epoch
    maker_epoch: u64,
    // Maker deep price
    maker_deep_price: OrderDeepPrice,
}
```

### Events

```rust
/// Emitted when a maker order is filled.
public struct OrderFilled has copy, store, drop {
    pool_id: ID,
    maker_order_id: u128,
    taker_order_id: u128,
    maker_client_order_id: u64,
    taker_client_order_id: u64,
    price: u64,
    taker_is_bid: bool,
    base_quantity: u64,
    quote_quantity: u64,
    maker_balance_manager_id: ID,
    taker_balance_manager_id: ID,
    timestamp: u64,
}

/// Emitted when a maker order is canceled.
public struct OrderCanceled<phantom BaseAsset, phantom QuoteAsset> has copy, store, drop {
    pool_id: ID,
    order_id: u128,
    client_order_id: u64,
    price: u64,
    is_bid: bool,
    base_asset_quantity_canceled: u64,
    timestamp: u64,
}

/// Emitted when a maker order is modified.
public struct OrderModified<phantom BaseAsset, phantom QuoteAsset> has copy, store, drop {
    pool_id: ID,
    order_id: u128,
    client_order_id: u64,
    price: u64,
    is_bid: bool,
    new_quantity: u64,
    timestamp: u64,
}

/// Emitted when a maker order is injected into the order book.
public struct OrderPlaced has copy, store, drop {
    balance_manager_id: ID,
    pool_id: ID,
    order_id: u128,
    client_order_id: u64,
    trader: address,
    price: u64,
    is_bid: bool,
    placed_quantity: u64,
    expire_timestamp: u64,
}
```

### Place Limit Order

The user must combine a `BalanceManager` call of generating a `TradeProof` before placing orders.

```rust
/// Place a limit order. Quantity is in base asset terms.
/// For current version pay_with_deep must be true, so the 
/// fee will be paid with DEEP tokens.
public fun place_limit_order<BaseAsset, QuoteAsset>(
    self: &mut Pool<BaseAsset, QuoteAsset>,
    balance_manager: &mut BalanceManager,
    trade_proof: &TradeProof,
    client_order_id: u64,
    order_type: u8,
    self_matching_option: u8,
    price: u64,
    quantity: u64,
    is_bid: bool,
    pay_with_deep: bool,
    expire_timestamp: u64,
    clock: &Clock,
    ctx: &TxContext,
): OrderInfo
```

### Place Market Order

```rust
/// Place a market order. Quantity is in base asset terms. Calls 
/// place_limit_order with a price of MAX_PRICE for bids and 
/// MIN_PRICE for asks. Any quantity not filled is cancelled.
public fun place_market_order<BaseAsset, QuoteAsset>(
    self: &mut Pool<BaseAsset, QuoteAsset>,
    balance_manager: &mut BalanceManager,
    trade_proof: &TradeProof,
    client_order_id: u64,
    self_matching_option: u8,
    quantity: u64,
    is_bid: bool,
    pay_with_deep: bool,
    clock: &Clock,
    ctx: &TxContext,
): OrderInfo
```

### Modify Order

`modify_order` does not return anything. If the transaction is successful, then assume the modification was successful.

```rust
/// Modifies an order given order_id and new_quantity.
/// New quantity must be less than the original quantity and more
/// than the filled quantity. Order must not have already expired.
public fun modify_order<BaseAsset, QuoteAsset>(
    self: &mut Pool<BaseAsset, QuoteAsset>,
    balance_manager: &mut BalanceManager,
    trade_proof: &TradeProof,
    order_id: u128,
    new_quantity: u64,
    clock: &Clock,
    ctx: &TxContext,
)
```

### Cancel Order

Similar to modify, `cancel_order` does not return anything.

```rust
/// Cancel an order. The order must be owned by the balance_manager.
/// The order is removed from the book and the balance_manager's open orders.
/// The balance_manager's balance is updated with the order's remaining quantity.
/// Order canceled event is emitted.
public fun cancel_order<BaseAsset, QuoteAsset>(
    self: &mut Pool<BaseAsset, QuoteAsset>,
    balance_manager: &mut BalanceManager,
    trade_proof: &TradeProof,
    order_id: u128,
    clock: &Clock,
    ctx: &TxContext,
)
```

### Withdraw Settled Amounts

All orders automatically withdraw settled amounts. This can be called explicitly to withdraw all settled funds from the pool.

```rust
/// Withdraw settled amounts to the `balance_manager`.
public fun withdraw_settled_amounts<BaseAsset, QuoteAsset>(
    self: &mut Pool<BaseAsset, QuoteAsset>,
    balance_manager: &mut BalanceManager,
    trade_proof: &TradeProof,
)
```